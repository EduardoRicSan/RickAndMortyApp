import java.util.Properties

plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.dagger.hilt)
    alias(libs.plugins.ksp)
}

android {
    namespace = "com.tech.data"
    compileSdk {
        version = release(36) {
            minorApiLevel = 1
        }
    }

    val baseUrl = project.findProperty("RICK_AND_MORTY_BASE_URL")?.toString()
        ?: System.getenv("RICK_AND_MORTY_BASE_URL")
        ?: ""

    if (baseUrl.isEmpty()) {
        logger.warn("CUIDADO: RICK_AND_MORTY_BASE_URL no está definida en gradle.properties ni en el sistema.")
    }

    defaultConfig {
        minSdk = 24

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles("consumer-rules.pro")
    }

    buildTypes {
        debug {
            buildConfigField("String", "RICK_AND_MORTY_BASE_URL", "\"$baseUrl\"")
        }
        release {
            buildConfigField("String", "RICK_AND_MORTY_BASE_URL", "\"$baseUrl\"")
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
    buildFeatures {
        buildConfig = true
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
}

dependencies {
    // Core Android
    implementation(libs.androidx.appcompat)
    implementation(libs.material)
    // Serialization
    implementation(libs.kotlinx.serialization.json)
    // Room
    implementation(libs.room.ktx)
    implementation(libs.room.paging)
    ksp(libs.room.compiler)
    // Hilt
    implementation(libs.dagger.hilt)
    ksp(libs.dagger.hilt.compiler)
    implementation(libs.hilt.navigation.compose)
    // Ktor
    implementation(libs.ktor.client.core)
    implementation(libs.ktor.client.android)
    implementation(libs.ktor.client.serialization)
    implementation(libs.ktor.client.logging)
    implementation(libs.ktor.client.content.negotiation)
    implementation(libs.ktor.serialization.json)
    // AndroidX
    implementation(libs.androidx.monitor)
    implementation(libs.androidx.junit.ktx)
    // Paging
    implementation(libs.androidx.paging.common)
    // Unit Test
    testImplementation(libs.junit)
    testImplementation(libs.androidx.junit)
    testImplementation(libs.mockk)
    testImplementation(libs.kotlinx.coroutines.test)
    testImplementation(libs.turbine)
    testImplementation(libs.ktor.client.mock)
    testImplementation(libs.androidx.room.testing)
    // Modules
    implementation(project(":core"))
}